<!DOCTYPE html>
<html>

<head>
  <meta charset="utf-8" />
  <title>Computer Graphics</title>
  <link href="https://fonts.googleapis.com/css?family=Lato&display=swap" rel="stylesheet">
  <link rel="stylesheet" href="styles.css">
</head>

<body>
  <div class="container">
    <div class="nav-wrapper">
      <div class="left-side">
        <div class="nav-link-wrapper">
          <a href="index.html">Home</a>
        </div>

        <div class="nav-link-wrapper">
          <a href="about.html">About</a>
        </div>
      </div>

      <div class="right-side">
        <div class="brand">
          <div>ERIK GYLLING</div>
        </div>
      </div>
    </div>

    <div class="content-wrapper">
      <div class="two-column-wrapper">
        <div class="project-image-wrapper" id="canvas" style="min-height:362px;">

        </div>

        <div class="project-content-wrapper">

          <div class="description">
            <h2>Ray Tracing assignment</h2>
            <p>This was an assignment in a computer graphics course at University of British Columbia.
            </p>
            <p>
              Pay attention to the moving sphere which is casting shadows. The ray tracing is limited to 3 bounces,
              which means it is possible to make an reflection inside of an reflection inside of another reflection.
              This is shown between the two reflecting spheres.
            </p>
            <h3>Languages & Technologies</h3>
            <ul>
              <li>JavaScript</li>
              <li>HTML</li>
            </ul>

            <h3>Controls</h3>
            <p>Move the light source by using W, A, S, D. (Only computer)
            </p>
          </div>
        </div>
      </div>
    </div>
  </div>


  <script src="js/three.js"></script>
  <script src="js/OBJLoader.js"></script>
  <script src="js/SourceLoader.js"></script>
  <script src="js/OrbitControls.js"></script>
  <script src="js/KeyboardState.js"></script> <!-- By Jerome Etienne: http://jetienne.com/ -->

  <script id="raytracerVertShader" type="x-shader/x-vertex">

void main()
{       
  gl_Position = projectionMatrix * modelViewMatrix * vec4(position,1.0);
}

</script>

  <script id="raytracerFragShader" type="x-shader/x-fragment">
precision highp float;
uniform vec2 resolution;
uniform float myFloat1;
uniform float myFloat2;
 uniform int maxBounces;
 uniform vec3 lightPosition;



const int n_spheres = 4;
float epsilon = 0.00001;
float largeT = 1000000.0;
vec3 backgroundColor = vec3(0.5,0.5,1.0);
mat4 planeMatrix = mat4(vec4(1,0,0,0),vec4(0,0,1,0),vec4(0,1,0,0),vec4(0,0,0,1));

struct light {
  vec3 color;
  vec3 position;
};

struct ray {
  vec3 origin;
  vec3 direction;
  int bounces;
};

struct material {
  float kLocal;
  float kReflect;
  vec3 color;
};

struct sphere {
  material mtrl;
  vec3 position;
  float radius;
  bool isLight;
};

struct plane {
  material mtrl;
  vec3 position;
  float width;
  float height;
};

sphere nearestSphere;
sphere scene_spheres[n_spheres];

/////////////////////////////////////////////////////////////////////////////////
// sphereIntersect():  returns smallest posistive t value for a sphere intersection;
//                     returns -1 if no intersection
/////////////////////////////////////////////////////////////////////////////////

float sphere_intersect(ray myRay, sphere sphr) {
  float a = dot(myRay.direction, myRay.direction);
  vec3 s0_r0 = myRay.origin - sphr.position;
  float b = 2.0 * dot(myRay.direction, s0_r0);
  float c = dot(s0_r0, s0_r0) - (sphr.radius * sphr.radius);
  float d = sqrt(b*b-4.0*a*c);               // compute the discriminant
  if (d < 0.0) {                 // no solution to the quadratic equation?
    return -1.0;                   // then flag as no intersection
  } else {
    float t1 = (-b - d)/(2.0*a);   // compute both values of t
    float t2 = (-b + d)/(2.0*a);
    float tmin = min(t1,t2);
    float tmax = max(t1,t2);
    if (tmax<=0.0+epsilon) return -1.0;
    if (tmin<=0.0+epsilon) return -1.0;
    return (tmin);              // return smallest positive value
  }
}

/////////////////////////////////////////////////////////////////////////////////
// nearestT():  cast a ray, and computes t for closest intersection in the direction of +t
//          If there is no intersection, it returns  largeT
/////////////////////////////////////////////////////////////////////////////////

float nearestT(ray myRay)
{
  float nearest_t = largeT;
  float curr_t;
  for (int i = 0; i < n_spheres; ++i) {
    curr_t = sphere_intersect(myRay, scene_spheres[i]);  // test against sphere
    if (curr_t == -1.0) continue;         // missed sphere?
    else if (curr_t < nearest_t) {        // closest sphere?
      nearest_t = curr_t;
      nearestSphere = scene_spheres[i];
    }
  }
  return nearest_t;
}

/////////////////////////////////////////////////////////////////////////////////
// localShade():  compute local color for a surface point
/////////////////////////////////////////////////////////////////////////////////

vec3 localShade(vec3 P, vec3 N, vec3 I, vec3 surfColor) {
  ray shadowRay;

/// TO DO:
//  (1) compute and return simple diffuse shading, i.e., (N.L)*surfColor,
//      where surfColor is the assigned color of the surface. Clamp values as appropriate.
  vec3 ambient = surfColor * 0.;

  vec3 L = -normalize(P-lightPosition);
  vec3 diffuse = 1.*dot(N,L)*surfColor;


  float n = 4.;
  vec3 ks = vec3(1,1,1);
  vec3 R = normalize(reflect(-L,N));
  vec3 V = normalize(vec3(0,0,0)-P);
  float rv = max(0.0,dot(R,V));
  float rvn = pow(rv,n);
  vec3 specular = surfColor*ks*rvn;

//      Note:  the assignment has been simplified -- you need only implement simple diffuse shading,
//      and Phong shading is not necessary. It can be added as an extra feature for the creative component
//      if you like.
//  (2) now additionally check to see if the object is in shadow by building and casting
//      a shadow ray.  If the point is in shadow, return black. Otherwise return the diffuse shading.

  shadowRay.direction = L;
  shadowRay.origin = P;
  float nearest_t = nearestT(shadowRay);

  if(!nearestSphere.isLight){
    return vec3(0,0,0);
  }

  return ambient + diffuse+specular;

}

/////////////////////////////////////////////////////////////////////////////////
// bgColor(ray):  cast a ray, and compute a color, recursively if needed
/////////////////////////////////////////////////////////////////////////////////

vec3 bgColor(ray myRay)
{
//  return backgroundColor;
  vec4 origin = planeMatrix*vec4(myRay.origin, 1.0);       // transform ray into the coord system of the plane
  vec4 direction = planeMatrix*vec4(myRay.direction,0.0);  //

  float zPlane = -10.0;               // in local coords, the plane occupies the xy-plane at z=-10
  float t = (zPlane - origin.z)/direction.z;    // intersect ray with plane, in local plane coords
  if (t<0.0) return backgroundColor;            // ray intersects behind the eye, so is looking away from the plane
  vec3 P = origin.xyz + t*direction.xyz;        // compute intersection point
  float xf = floor(fract(0.1*P.x)+0.5);   // 0 or 1    computations to compute checkerboard pattern
  float yf = floor(fract(0.1*P.y)+0.5);   // 0 or 1
  float sum = xf + yf;
  if (sum==1.0)
    return vec3(0.3,0.3,0.3);      // black square
  else
    return vec3(1,1,1);            // white square
}

/////////////////////////////////////////////////////////////////////////////////
// rayCast3():  cast the reflected ray, and compute a color for it
/////////////////////////////////////////////////////////////////////////////////

vec3 rayCast3(ray myRay)             // return the color for this reflected ray
{

 float nearest_t = nearestT(myRay);              // find closest object
 if (nearest_t < largeT) {       // hit an object?

  vec3 P = myRay.origin + nearest_t*myRay.direction;
  vec3 N = normalize(P-nearestSphere.position);
  vec3 I = -normalize(myRay.direction);

  if(nearestSphere.isLight==true){
    return nearestSphere.mtrl.color;
  }
  vec3 localColor = localShade(P,N,I,nearestSphere.mtrl.color);  // local shading

  return localColor;
 }

  return bgColor(myRay);       // return checkeboard texture
}


/////////////////////////////////////////////////////////////////////////////////
// rayCast2():  cast the reflected ray, and compute a color for it
/////////////////////////////////////////////////////////////////////////////////

vec3 rayCast2(ray myRay)             // return the color for this reflected ray
{


  float nearest_t = nearestT(myRay);              // find closest object
  float kReflect = nearestSphere.mtrl.kReflect;
  float kLocal = nearestSphere.mtrl.kLocal;
 if (nearest_t < largeT) {       // hit an object?

  vec3 P = myRay.origin + nearest_t*myRay.direction;
  vec3 N = normalize(P-nearestSphere.position);
  vec3 I = -normalize(myRay.direction);

  if(nearestSphere.isLight==true){
    return nearestSphere.mtrl.color;
  }
  vec3 localColor = localShade(P,N,I,nearestSphere.mtrl.color);  // local shading

  ray reflectedRay;
  reflectedRay.origin = P;
  reflectedRay.direction = reflect(-I,N);
  reflectedRay.bounces = myRay.bounces - 1;


    vec3 reflectedColor = rayCast3(reflectedRay);


    return (kLocal*localColor + kReflect*reflectedColor);

 }

  return bgColor(myRay);       // return checkeboard texture
}

/////////////////////////////////////////////////////////////////////////////////
// rayCast():  cast a ray, and compute a color, recursively if needed
/////////////////////////////////////////////////////////////////////////////////

vec3 rayCast(ray myRay)             // return color
{
  float nearest_t = nearestT(myRay);              // find closest object
  float kReflect = nearestSphere.mtrl.kReflect;         // keep these lines for use in step (7)
  float kLocal = nearestSphere.mtrl.kLocal;       // keep these lines for use in step (7)
  if (nearest_t < largeT) {       // hit an object?

  vec3 P = myRay.origin + nearest_t*myRay.direction;
  vec3 N = normalize(P-nearestSphere.position);
  vec3 I = -normalize(myRay.direction);

  if(nearestSphere.isLight==true){
    return nearestSphere.mtrl.color;
  }
  vec3 localColor = localShade(P,N,I,nearestSphere.mtrl.color);  // local shading

  ray reflectedRay;
  reflectedRay.origin = P;
  reflectedRay.direction = reflect(-I,N);
  reflectedRay.bounces = myRay.bounces - 1;


    vec3 reflectedColor = rayCast2(reflectedRay);

  ray refractedRay;
  refractedRay.origin = P;
  refractedRay.direction = refract(-I,N,1.);
  refractedRay.bounces = myRay.bounces - 1;
  vec3 refractedColor = rayCast2(refractedRay);

    return (kLocal*localColor + kReflect*reflectedColor + 0.3*refractedColor);

  }
  return bgColor(myRay);       // return background checkerboard or sky color if no objects hit
}

/////////////////////////////////////////////////////////////////////////////////
// initialize():  define the scene:  a set of spheres
/////////////////////////////////////////////////////////////////////////////////

void initialize()
{
  // INIT SPHERES

  sphere sphere0;
  sphere0.mtrl.color = vec3(1.0, 1.0, 0.0);
  sphere0.mtrl.kReflect = 0.0;
  sphere0.mtrl.kLocal = 1.0;
  sphere0.radius = 0.1;
  sphere0.position = lightPosition;
  sphere0.isLight = true;

  sphere sphere1;
  sphere1.mtrl.color = vec3(0.5, 1, 0.5);
  sphere1.mtrl.kReflect = 0.6;
  sphere1.mtrl.kLocal = 0.5;
  sphere1.radius = 2.0;
  sphere1.position = vec3(-1,-2.0,-10);
  sphere1.isLight = false;

  sphere sphere2;
  sphere2.mtrl.color = vec3(1.0, 0.0, 0.0);
  sphere2.mtrl.kReflect = 0.0;
  sphere2.mtrl.kLocal = 1.0;
  sphere2.radius = 1.0;
  sphere2.position = vec3(0,3,-8);
  sphere2.isLight = false;

  sphere sphere3;
  sphere3.mtrl.color = vec3(1.0, 0.5, 1.0);
  sphere3.mtrl.kReflect = 0.2;
  sphere3.mtrl.kLocal = 0.8;
  sphere3.radius = 1.5;
  sphere3.position = vec3(1,1,-7.5);
  sphere3.position.x = 1.0 + myFloat1;
  sphere3.position.y = 1.0 + myFloat2;
  sphere3.isLight = false;

  scene_spheres[0] = sphere0;
  scene_spheres[1] = sphere1;
  scene_spheres[2] = sphere2;
  scene_spheres[3] = sphere3;
}

/////////////////////////////////////////////////////////////////////////////////
// main()
/////////////////////////////////////////////////////////////////////////////////

void main() {
  initialize();   // define the world geometry, i.e., four spheres

    // compute normalized screen coordinates for pixel:  [-1,1] in y;  [-a,a] in x, a=aspect ratio
  float a = resolution.x/resolution.y;
  vec2 uv = (-1.0 + 2.0*gl_FragCoord.xy / resolution.xy) * vec2(a, 1.0);

   // define the ray (in VCS) that corresponds to the current fragment
   // P(0,0,0) --> P(x,y,-1.0)    point on the plane z=-1.0, where x=uv[0], y=uv[1]
  ray pixelRay;
  float sf = 0.5;    // scale factor that controls field-of-view
  pixelRay.origin = vec3(0,0,0);                       // ray starts at eye:  origin
  pixelRay.direction = normalize(vec3(sf*uv,-1.0));    // look in the direction of a given pixel

  vec3 rayColor = rayCast(pixelRay);            // cast the initial ray
  gl_FragColor = vec4(rayColor, 1.0);           // assign color to fragment

}
</script>

  <script src="a6.js"></script>

</body>

</html>